# [0] += [1]
# [1] = 0
friends can_do_addition
    tanoshi!
    uwa~
        sugo-i!
        ta-noshi!
    wai~

# [0] *= [1]
friends can_do_multiplication
    shaberu
    tanoshi!
    sugo-i
    la
    sugoi!!
    la
    tanoshi!
    uwa~
        sugoi!!
        sandstar can_do_addition
        tanoshi!
        shabetta
        tanoshi!
        sugo-i
    wai~

# Print [0] to number.
# Only 0~99
friends can_say_number
    tanoshi!
    '0'
    tanoshi!
    # This friends not so smart.
    " 0  1  2  3  4  5  6  7  8  9  "
    "10 11 12 13 14 15 16 17 18 19  "
    "20 21 22 23 24 25 26 27 28 29  "
    "30 31 32 33 34 35 36 37 38 39  "
    "40 41 42 43 44 45 46 47 48 49  "
    "50 51 52 53 54 55 56 57 58 59  "
    "60 61 62 63 64 65 66 67 68 69  "
    "70 71 72 73 74 75 76 77 78 79  "
    "80 81 82 83 84 85 86 87 88 89  "
    "90 91 92 93 94 95 96 97 98 99  "
    uwa~
        sugoi!
    wai~
    sugoi!
    shaberu
    tanoshi!
    '1'
    uwa~
        tanoshi!!!
        shabetta
        sugo-i
        shaberu
    wai~
    tanoshi!
    omoshiro!!

# Welcome to Japari-Math!
youkoso
    '0 9 8 7 6 5 4 3 2'
    uwa~~
        la la la
        sugoi!!
        '2'
        u----------wa~
            la
            sugoi!!
            shaberu
            tanoshi!!!!!!
            shabetta
            sandstar can_say_number
            "x"
            omoshiro!
            sugoi!!!!
            shaberu
            tanoshi!!!!!!!
            shabetta
            sandstar can_say_number
            "="
            omoshiro!
            sugoi!!!!!!!
            sandstar can_do_multiplication
            sandstar can_say_number
            '10' # new line
            omoshiro!
            sugoi!!
            shaberu
            tanoshi!!!
            shabetta
            sugoi!!
            ta-noshi
        wa----------i~
        sugoi!!
    wai~~